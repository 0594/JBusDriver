apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"
    defaultConfig {
        applicationId "me.jbusdriver"
        minSdkVersion 16
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            try {
                storeFile file(KEYSTORE_FILE)
                storePassword KEYSTORE_PASSWORD
                keyAlias KEYSTORE_ALIAS
                keyPassword KEY_PASSWORD
            }
            catch (ignored) {
                if (System.getenv("KEYSTORE_FILE") != null) {
                    storeFile = file(System.getenv("KEYSTORE_FILE"))
                    storePassword = System.getenv("KEYSTORE_PWD")
                    keyAlias = System.getenv("KEYSTORE_ALIAS")
                    keyPassword = System.getenv("KEYSTORE_ALIAS_PWD")
                } else {
                    throw new InvalidUserDataException("You should define KEYSTORE_PASSWORD and KEY_PASSWORD in gradle.properties.")
                }
            }
        }
    }
    lintOptions {
        abortOnError false
    }
    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            applicationVariants.all { variant ->
                if (variant.buildType.name == 'release') {
                    variant.mergedFlavor.versionName = VersionName()
                    variant.mergedFlavor.versionCode = VersionCode()
                    renameArtifact(variant)
                }

            }

        }

        debug {
            signingConfig signingConfigs.release
        }
    }
    productFlavors {
        gayhub {
            manifestPlaceholders = [UMENG_KEY: "58e8ecaa7666134a5a001740",
                                    CHANNEL  : "gayhub"]
        }
    }
}

def releaseTime() {
    return new Date().format("yyyyMMdd_HHmmss")
}

//"HelloWord-${variant.buildType.name}-v${variant.versionName}-${variant.productFlavors[0].name}-${releaseTime()}.apk".toLowerCase())
def renameArtifact(variant) {
    variant.outputs.each { output ->
        output.outputFile = new File(
                (String) output.outputFile.parent,
                (String) "JubsDriver[v${variant.versionName}]_(code_${variant.versionCode}).apk")
    }
}

//使用git的Tag来自动定制Version
def VersionCode() {
    def cmd = 'git rev-list master --first-parent --count'//master分支
//    def cmd = 'git tag'
//    def code = cmd.execute().text.trim().split("\n").size()
    def code = cmd.execute().text.trim().toInteger()
    logger.quiet(cmd + " >>>> code = " + code)
    return code
}

//使用git的Tag来自动定制Version
def VersionName() {
    def cmd = 'git tag'
    def tags = cmd.execute().text.readLines()
    /* if (tags.size() >= 1) {
         def version_last = tags.last().trim() //newest
         cmd = 'git rev-list ' + version_last + '..  --count'
         if (tags.size() >= 2) {
             tags.remove(tags.last())
             def version_last_2 = tags.last().trim() //newest
             cmd = 'git rev-list ' + version_last_2 + '..' + version_last + '  --count'
         }
     } else {
         cmd = 'git rev-list  --count'
     }*/
    cmd = 'git rev-list  --all  --count'
    def code = cmd.execute().text.trim().toInteger()

    def versionName = tags.last().trim().replaceAll("[a-zA-Z]", "") + "build" + code
    logger.quiet(cmd + " >>>> versionName  = " + versionName)

    return versionName
}



dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile 'com.android.support:appcompat-v7:25.3.1'
    compile 'com.android.support:design:25.3.1'
    compile 'com.android.support:cardview-v7:25.3.1'
    compile 'com.android.support:palette-v7:25.3.1'
    testCompile 'junit:junit:4.12'
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    /*============================================added======================================================*/
    compile 'com.google.code.gson:gson:2.8.0'
    compile 'com.afollestad.material-dialogs:core:0.9.4.4'
    compile 'com.orhanobut:logger:1.15'
    compile 'com.github.CymChad:BaseRecyclerViewAdapterHelper:2.9.+'
    compile group: 'org.jsoup', name: 'jsoup', version: '1.10.2'

    compile 'com.squareup.okhttp3:okhttp:3.8.0'
    compile 'com.github.bumptech.glide:glide:3.8.0'
    compile 'com.github.bumptech.glide:okhttp3-integration:1.5.0@jar'

    compile 'com.squareup.retrofit2:retrofit:2.2.0'
//    compile 'com.squareup.retrofit2:converter-gson:2.2.0'
    compile 'com.squareup.retrofit2:adapter-rxjava2:2.2.0'


    compile 'io.reactivex.rxjava2:rxjava:2.1.0'
    compile 'io.reactivex.rxjava2:rxandroid:2.0.1'
    compile 'io.reactivex.rxjava2:rxkotlin:2.0.2'
    compile 'com.tbruyelle.rxpermissions2:rxpermissions:0.9.3@aar'

    compile 'com.jaeger.statusbarutil:library:1.4.0'
    compile 'com.xiaofeng.android:flowlayoutmanager:1.2.3.2'
    compile 'com.umeng.analytics:analytics:latest.integration'

}
repositories {
    mavenCentral()
}
